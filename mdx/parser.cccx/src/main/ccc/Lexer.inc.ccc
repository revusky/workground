/*
* Copyright (c) 2023 Contributors to the Eclipse Foundation.
*
* This program and the accompanying materials are made
* available under the terms of the Eclipse Public License 2.0
* which is available at https://www.eclipse.org/legal/epl-2.0/
*
* SPDX-License-Identifier: EPL-2.0
*
* Contributors:
*   SmartCity Jena - initial
*   Stefan Bischof (bipolis.org) - initial
*/

INJECT LEXER_CLASS :
{
  // It seems that multiline comments can nest 
  // in MDX. So we have this little TOKEN_HOOK
  // and these variables where we keep track
  // of the nesting levels of the comments and
  // where the outermost comment began.
  private int commentNesting, commentStartOffset;

  private BASE_TOKEN_CLASS TOKEN_HOOK(BASE_TOKEN_CLASS tok) {
    if (tok.getType() == SLASH_STAR) {
      if (commentNesting==0) {
        commentStartOffset=tok.getBeginOffset();
      } else {
        tok.setBeginOffset(commentStartOffset);
      }
      commentNesting++;
    }
    else if (tok.getType() == MULTI_LINE_COMMENT) {
      --commentNesting;
      tok.setBeginOffset(commentStartOffset);
      if (commentNesting > 0) {
         switchTo(LexicalState.IN_MULTI_LINE_COMMENT);
      } 
    }
    else if (tok.getType() == EOF && commentNesting !=0) {
      tok.setBeginOffset(commentStartOffset);
      tok.setType(INVALID);
    }
    return tok;
  }
}

TOKEN :
  < AND : "AND" >
| < AS : "AS" >
| < AXIS : "AXIS" >
| < BEGIN : "BEGIN" >
| < BY : "BY" >
| < CASE : "CASE" >
| < CALCULATION : "CALCULATION" >
| < CAST : "CAST" >
| < CELL : "CELL" >
| < CHAPTERS : "CHAPTERS" >
| < CREATE : "CREATE" >
| < COLUMNS : "COLUMNS" >
| < COMMIT : "COMMIT" >
| < CUBE : "CUBE" >
| < CURRENTCUBE : "CURRENTCUBE" >
| < DIMENSION : "DIMENSION" >
| < DRILLTHROUGH : "DRILLTHROUGH" >
| < ELSE : "ELSE" >
| < EMPTY : "EMPTY" >
| < END : "END" >
| < EXPLAIN : "EXPLAIN" >
| < FIRSTROWSET : "FIRSTROWSET" >
| < FOR : "FOR" >
| < FROM : "FROM" >
| < IN : "IN" >
| < IS : "IS" >
| < MATCHES : "MATCHES" >
| < MAXROWS : "MAXROWS" >
| < MEMBER : "MEMBER" >
| < MEASURE : "MEASURE" >
| < NON : "NON" >
| < NOT : "NOT" >
| < NULL : "NULL" > #NullLiteral
| < ON : "ON" >
| < OR : "OR" >
| < PAGES : "PAGES" >
| < PLAN : "PLAN" >
| < PROPERTIES : "PROPERTIES" >
| < REFRESH : "REFRESH" >
| < RETURN : "RETURN" >
| < ROLLBACK : "ROLLBACK" >
| < ROWS : "ROWS" >
| < SECTIONS : "SECTIONS" >
| < SELECT : "SELECT" >
| < SESSION : "SESSION" >
| < SET : "SET" >
| < THEN : "THEN" >
| < TRAN : "TRAN" >
| < TRANSACTION : "TRANSACTION" >
| < UPDATE : "UPDATE" >
| < USE_EQUAL_ALLOCATION : "USE_EQUAL_ALLOCATION" >
| < USE_EQUAL_INCREMENT : "USE_EQUAL_INCREMENT" >
| < USE_WEIGHTED_ALLOCATION : "USE_WEIGHTED_ALLOCATION" >
| < USE_WEIGHTED_INCREMENT : "USE_WEIGHTED_INCREMENT" >
| < WHEN : "WHEN" >
| < WHERE : "WHERE" >
| < XOR : "XOR" >
| < WITH : "WITH" >
| < EXISTING : "EXISTING" >
| < SYSTEM_CUBE : "$SYSTEM" >
;


// White space
SKIP :
  " "
| "\t"
| "\n"
| "\r"
| "\f"
;


UNPARSED : 
  < SINGLE_LINE_COMMENT : ("//" | "--") (~["\n"])* "\n" > 
;

<DEFAULT, IN_MULTI_LINE_COMMENT>
UNPARSED :
  <SLASH_STAR : "/*"> : IN_MULTI_LINE_COMMENT
;

< IN_MULTI_LINE_COMMENT >
UNPARSED :
  < MULTI_LINE_COMMENT : "*/" > : DEFAULT
;

< IN_MULTI_LINE_COMMENT >
SKIP :
  < ~[ ] >
;

/*
 * Separators
 */
< DEFAULT >
TOKEN :
  < LPAREN : "(" >
| < RPAREN : ")" >
| < LBRACE : "{" >
| < RBRACE : "}" >
| < LBRACKET : "[" >
| < RBRACKET : "]" >
| < COMMA : "," >
| < SEMICOLON : ";" >
| < DOT : "." >
;


/*
 * Operators
 */
< DEFAULT >
TOKEN :
  < ASTERISK : "*" >
| < BANG : "!" >
| < COLON : ":" >
| < CONCAT : "||" >
| < EQ : "=" >
| < GE : ">=" >
| < GT : ">" >
| < LE : "<=" >
| < LT : "<" >
| < MINUS : "-" >
| < NE : "<>" >
| < PLUS : "+" >
| < SOLIDUS : "/" >
| < ATSIGN : "@" >
;

INCLUDE JAVA_IDENTIFIER_DEF
/*
 * Identifiers
 * - https://javacc.github.io/javacc/documentation/bnf.html#identifiers
 */

TOKEN #Identifier :
  < ID : <JAVA_IDENTIFIER_START> (<JAVA_IDENTIFIER_PART>)* > 
  |
  < QUOTED_ID :
    < LBRACKET >
    (
      (~[ "]", "\n", "\r" ])
    | ("]]")
    )*
    < RBRACKET >
  >
  |
  < AMP_QUOTED_ID : "&" < QUOTED_ID > >
  |
  < AMP_UNQUOTED_ID :
    "&" [ "a"-"z", "A"-"Z" ]
    ( <JAVA_IDENTIFIER_PART> )*
  >
;

TOKEN #NumericLiteral :
  < UNSIGNED_INTEGER_LITERAL : ([ "0"-"9" ])+ >
  |
  < APPROX_NUMERIC_LITERAL :
    (
      < UNSIGNED_INTEGER_LITERAL >
      | 
      < DECIMAL_NUMERIC_LITERAL >
    )
    < EXPONENT > 
  > 
  |
  < DECIMAL_NUMERIC_LITERAL :
    ([ "0"-"9" ])+ (".")? ([ "0"-"9" ])*
  | "." ([ "0"-"9" ])+
  > 
  |
  < #EXPONENT : [ "e", "E" ] ([ "+", "-" ])? ([ "0"-"9" ])+ >
;

TOKEN #StringLiteral : 
  < SINGLE_QUOTED_STRING :
    "'"
    (
      (~[ "'" ])
    | ("''")
    )*
    "'" >
  |
  < DOUBLE_QUOTED_STRING :
    "\""
    (
      (~[ "\"" ])
    | ("\"\"")
    )*
    "\"" 
  >
;

INJECT ASTStringLiteral :
   import static PARSER_PACKAGE.PARSER_CLASS.stripQuotes;
   implements org.eclipse.daanse.mdx.model.api.expression.StringLiteral 
{
   @Property String value;
   public String value() {
      return getValue();
   }

   public void close() {
      String image = getImage();
      if (image.charAt(0) == '\'') 
         value = stripQuotes(image, "'", "'", "''");
      else 
         value = stripQuotes(image, "\"", "\"", "\"\"");
   }
}

INJECT ASTNumericLiteral : 
  import java.math.BigDecimal;
  implements org.eclipse.daanse.mdx.model.api.expression.NumericLiteral
{
   public BigDecimal value() {
      return new BigDecimal(getImage());
   }
}

INJECT ASTNullLiteral : implements org.eclipse.daanse.mdx.model.api.expression.NullLiteral;